#!/usr/bin/env bb
(ns artur.webhook
  (:require
    [babashka.curl :as curl]
    [clojure.tools.cli :as cli]))

(def cli-opts
  [["-p" "--port PORT" "Port number"
    :default 3003
    :parse-fn #(Integer/parseInt %)
    :validate [#(< 0 % 0x10000) "Must be a number between 0 and 65536"]]
   ["-u" "--url URL" "Base URL"
    :default "http://localhost"]
   ["-i" "--headers" "Print the response headers"]
   ["-h" "--help"]])

(defn do-request [{:keys [url port]} text]
  (curl/post (str url ":" port) {:form-params {"Body" text}}))

(defn -main [& args]
  (let [{:keys [arguments options summary errors]}
        (cli/parse-opts args cli-opts)]
    (cond
      (seq errors) (doall (for [e errors] (println e)))
      (:help options) (println summary)
      :else
      (let [{:keys [headers body] :as res}
            (do-request options (apply str arguments))]
        (when (:headers options)
          (doall (for [[k v] headers]
                   (printf "%s: %s\n" k v)))
          (println))
        (println body)))
    nil))

(apply -main *command-line-args*)
